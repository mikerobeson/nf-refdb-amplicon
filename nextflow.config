nextflow.enable.dsl = 2

// Determine necessary QIIME 2 Conda envs to incorporate.
// Taken from nf-ducken.
sys_properties = System.getProperties()
executor_sys  = sys_properties['os.name'].toLowerCase()
if (executor_sys.contains("mac")) {
    sys_abbreviation = "osx"
} else {
    sys_abbreviation = "linux"   // Note Windows users need QIIME 2 on WSL
}

params.qiime_release = '2024.5-py39'
params.qiime_conda_env  = "${baseDir}/assets/qiime2-amplicon-${params.qiime_release}-${sys_abbreviation}-conda.yml"
// params.qiime_conda_env   = "/Users/robesonmichael/AnalysisSoftware/miniconda3/envs/qiime2-amplicon-2024.5"

params.primer_pairs = [
    ['27F338R', 'AGAGTTTGATYMTGGCTCAG', 'GCTGCCTCCCGTAGGAGT'],
    ['27F534R', 'AGAGTTTGATYMTGGCTCAG', 'ATTACCGCGGCTGCTGG'],
    ['357wF805R', 'CCTACGGGNGGCWGCAG',	'GACTACHVGGGTATCTAATCC'],
    ['357wF806R', 'CCTACGGGNGGCWGCAG', 'GGACTACHVGGGTWTCTAAT'],
    ['515F806R', 'GTGYCAGCMGCCGCGGTAA', 'GGACTACNVGGGTWTCTAAT'],
    ['515F926R', 'GTGYCAGCMGCCGCGGTAA', 'CCGYCAATTYMTTTRAGTTT'],
    ['515F944R', 'GTGCCAGCMGCCGCGGTAA', 'GAATTAAACCACATGCTC'],
    ['939F1378R', 'GAATTGACGGGGGCCCGCACAAG', 'CGGTGTGTACAAGGCCCGGGAACG'],
]

params.outdir = 'results'

params {

    get_silva {
        version   = '138.1'
        target    = 'SSURef_NR99'
        ranks     = 'domain phylum class order family genus'
    }

    get_gtdb {
        version   = '214.1'
        domain    = 'Both'
        dbtype    = 'SpeciesReps'
    }

    derep {
        mode      = 'uniq'
    }

}


process {

    withName: '.*_GTDB' {
        publishDir = [
            path: "${params.outdir}/gtdb",
            mode: 'copy',
        ]
    }

    withName: '.*_RDP' {
        publishDir = [
            path: "${params.outdir}/rdp",
            mode: 'copy',
        ]
    }

    withName: '.*_SILVA' {
        publishDir = [
            path: "${params.outdir}/silva",
            mode: 'copy',
        ]
    }

    withLabel: 'get_silva|get_gtdb|get_rdp' {
        // Comment the `executor` line below if your HPC worker nodes
        // can access the internet. This will submit these tasks to
        // your job schedular. Otherwise, leave uncommented if your
        // HPC can't access the internet and it will the processes
        // with the specified labels on the login / head node.
        executor   = 'local'
        cpus       = 1
        memory     = '2 GB'
        queue      = 'xeon'
        time       = '1h'
    }


    withLabel: 'derep' {
        cpus       = 8
        memory     = '32 GB'
        queue      = 'xeon'
        time       = '2h'
    }

    withLabel: 'amp_reg_extract' {
        cpus       = 8
        memory     = '32 GB'
        queue      = 'xeon'
        time       = '4h'
    }

    withLabel: 'train_classifier' {
        cpus       = 1
        memory     = '64 GB'
        queue      = 'xeon'
        time       = '36h'
    }

}


profiles {

    standard {
        process.executor    = 'local'
        conda.enabled       = true
        process.conda       = params.qiime_conda_env
        executor.cpus       = 8
        executor.memory     = '64 GB'
    }

    cluster {
        process.executor            = 'slurm'
        executor.submitRateLimit    = '6/1min'
        executor.queueSize          = 20
        process.queue               = 'xeon'
        conda.enabled               = true
        process.conda               = params.qiime_conda_env
    }

}
